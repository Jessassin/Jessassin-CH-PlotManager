# +----------------------------------------------+
# | Jessassin's Plot Management System           |
# | Main.msa                                     |
# +----------------------------------------------+

all:/signtest [$peram] = >>>
if(equals_ic($peram,'a'),
	set_sign_text(pinfo(player())[2],concat('§a','[Available]'),concat('§b','[Plot: 10101]'),concat())
)
if(equals_ic($peram,'t'),
	set_sign_text(pinfo(player())[2],concat('§4','[Taken]'),concat('§b','[Plot: 10101]'),concat('§1',player()),concat('§9','Mai Motto!'))
)
if(equals_ic($peram,'r'),
	set_sign_text(pinfo(player())[2],concat('§e','[Reserved]'),concat('§b','[Plot: 10101]'),concat('§1',player()),concat('§9','Mai Motto!'))
)
<<<

all:/maptest [$address] [$color] = >>>
assign(@red,'35:14')
assign(@green,'35:5')
if(equals_ic($color,'green'),
	assign(@color,@green),
	assign(@color,@red)
)
assign(@addresscoords,_plot_address_coordinates($address))
#msg(concat('Quadrant: ',@addresscoords[0]))
#msg(concat('X: ',@addresscoords[1]))
#msg(concat('Z: ',@addresscoords[2]))
assign(@newaddresscoords,_map_location($address))
#msg(concat('Quadrant: ',@newaddresscoords[0]))
#msg(concat('New X: ',@newaddresscoords[1]))
#msg(concat('New Y: ',@newaddresscoords[2]))
#msg(concat('Point2: ',@newaddresscoords[3]))
assign(@point2,@newaddresscoords[3])
assign(@point1,array(@newaddresscoords[1],@point2[1],@newaddresscoords[2]))
msg(concat('Point 1: ',@point1))
msg(concat('Point 2: ',@point2))
sk_pos1(@point1)
sk_pos2(@point2)
run(concat('//set ',@color)) 
run('//sel')
<<<

all:/plot [$peram1=''] [$peram2=''] [$peram3=''] = >>>
	assign(@player,player())
#	if(
#		not(has_permission(@player,'jessassin.plot.command')),
#		die('You do not have permission to run any /plot commands!')
#	)
	include('/includes/options.ms')
	assign(@command,'/plot')
	assign(@syntax,'<peram1> <peram2> <peram3>')
	assign(@peram1,$peram1)
	assign(@peram2,$peram2)
	assign(@peram3,$peram3)
	if(
		lt(
			length(@peram1),1),
			die(_syntaxerror(@command,@syntax))
	)
	assign(@quadrant,_currentquadrant(pinfo(player(),1)))
	assign(@playerpos,pinfo(@player,1))
	assign(@playerx,@playerpos[0])
	assign(@playerz,@playerpos[2])
	assign(@address,_plotinfo(@playerpos)[3])
	assign(@peram1options,array('claim','unclaim','generate','info','playerinfo','member','guest'))
	if(
		not(
			array_contains_ic(@peram1options,@peram1)),
		die(_syntaxmessage(@command,'<claim,unclaim,generate,info,playerinfo,member,guest> <peram2>'))
	)
	if(
		equals_ic(@peram1,'claim'),
			if(
				not(has_permission(@player,'jessassin.plot.claim')),
				die('You do not have permission to do this!')
			)
			assign(@errormessage,_claimplot(@playerpos,@player))
			if(
				gt(length(@errormessage),0),
					msg(@errormessage),
					msg('Plot claimed!')
			)
	)
	if(
		equals_ic(@peram1,'unclaim'),
			assign(@errormessage,_unclaimplot(@playerpos,@player))
			if(
				gt(length(@errormessage),0),
					msg(@errormessage),
					msg('Plot unclaimed!')
			)
	)
	if(
		equals_ic(@peram1,'generate'),
			if(
				not(has_permission(@player,'jessassin.plot.generate')),
					die('You do not have permission to do this!')
			)
			assign(@starttime,nano_time())
			assign(@colorarray,array('35:8','35:7','35:15'))
			assign(@maxcolors,length(@plotcolors))
			assign(@color1,@plotcolors[rand(0,subtract(@maxcolors,1))])
			assign(@color2,@plotcolors[rand(0,subtract(@maxcolors,1))])
			assign(@color3,@plotcolors[rand(0,subtract(@maxcolors,1))])
			assign(@colorarray,array(@color1,@color2,@color3))
			
			_Generate_Plot(@playerpos,@colorarray)
			assign(@endtime,nano_time())
			assign(@totaltime,subtract(@endtime,@starttime))
			msg(concat('Total Time: ',@totaltime,' NanoSeconds'))
			msg(concat('Total Time: ',ceil(divide(@totaltime,1000000000)),' Seconds'))
	)
	if(
		equals_ic(@peram1,'info'),
#			if(
#				not(has_permission(@player,'jessassin.plot.info')),
#					die('You do not have permission to do this!')
#			)
			assign(@plotinfo,_plotinfo(@playerpos))
			if(
				lt(length(@plotinfo[4]),1),
					assign(@owner,'No owner'),
					assign(@owner,@plotinfo[4])
			)
			if(
				lt(length(@plotinfo[5]),1),
					assign(@memberarray,'No members'),
					assign(@memberarray,@plotinfo[5])
			)
			if(
				lt(length(@plotinfo[6]),1),
					assign(@guestarray,'No guests'),
					assign(@guestarray,@plotinfo[6])
			)
			msg()
			msg('Plot information:')
			msg(concat('Quadrant: ',@plotinfo[0]))
			msg(concat('Coordinate 1: ',@plotinfo[1]))
			msg(concat('Coordinate 2: ',@plotinfo[2]))
			msg(concat('Address: ',@plotinfo[3]))
			msg(concat('Owner: ',@owner))
			msg(concat('Members: ',@memberarray))
			msg(concat('Guests: ',@guestarray))
			msg()
	)
	if(
		equals_ic(@peram1,'playerinfo'),
			if(
				not(has_permission(@player,'jessassin.plot.playerinfo')),
				die('You do not have permission to do this!')
			)
			if(
				lt(length(@peram2),1),
					assign(@player,player()),
					assign(@player,@peram2)
			)
			assign(@playerplotinfo,_get_player_plotinfo(@player))
			msg()
			msg(concat('Player information for ',@player))
			msg(concat('Owned plots: ',@playerplotinfo[0]))
			msg(concat('Number of owned plots: ',@playerplotinfo[1]))
			msg(concat('Number of avaliable plots: ',@playerplotinfo[2]))
			msg()
	)
	if(
		equals_ic(@peram1,'member'),
			if(
				not(has_permission(@player,'jessassin.plot.member')),
				die('You do not have permission to do this!')
			)
			if(
				and(
					not(equals_ic(@peram2,'add')),
					not(equals_ic(@peram2,'remove'))
				),
				msg('Missing or invalid perameter!')
				die('/plot member <add/remove>')
			)
			if(
				lt(length(@peram3),3),
				msg('Missing or invalid player!')
				die('/plot member <add/remove> <player>')
			)
			if(equals_ic(@peram2,'add'),
				msg(_plot_addmember(@address,@peram3,@player)),
				msg(_plot_removemember(@address,@peram3,@player))
			)
	)
	if(
		equals_ic(@peram1,'guest'),
			if(
				not(has_permission(@player,'jessassin.plot.guest')),
				die('You do not have permission to do this!')
			)
			if(
				and(
					not(equals_ic(@peram2,'add')),
					not(equals_ic(@peram2,'remove'))
				),
				msg('Missing or invalid perameter!')
				die('/plot guest <add/remove>')
			)
			if(
				lt(length(@peram3),3),
				msg('Missing or invalid player!')
				die('/plot guest <add/remove> <player>')
			)
			if(equals_ic(@peram2,'add'),
				msg(_plot_addguest(@address,@peram3,@player)),
				msg(_plot_removeguest(@address,@peram3,@player))
			)
	)
<<< 